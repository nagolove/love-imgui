PROJECT(imgui-bindings)
CMAKE_MINIMUM_REQUIRED(VERSION 2.8)

OPTION(EnableCodeGen "automatically recompile the code-generated imgui header. requires luajit." ON)
OPTION(EnableLove "build the love-imgui bindings" ON)

SET(CMAKE_MODULE_PATH ${PROJECT_SOURCE_DIR}/cmake)

IF(EnableCodeGen)
	find_package(LuaJIT REQUIRED)
	SET(BindSource
		bindings2/generate_bindings.lua
		bindings2/parse.lua
		bindings2/util.lua
		bindings2/buffer.lua

		bindings2/lua/types.lua
		bindings2/lua/generate.lua
		bindings2/lua/generate_autocomplete.lua
		bindings2/lua/wrap_imgui_codegen.cpp
		bindings2/lua/wrap_imgui_codegen.h
		bindings2/lua/api_details.md
		bindings2/lua/autocomplete.json

		deps/libimgui/imgui.h
		deps/libimgui/misc/cpp/imgui_stdlib.h
	)
	SET(BindOutput
		${CMAKE_SOURCE_DIR}/src/lua-bindings/wrap_imgui_codegen.h
		${CMAKE_SOURCE_DIR}/src/lua-bindings/wrap_imgui_codegen.cpp
		${CMAKE_SOURCE_DIR}/src/lua-bindings/api_details.md
		${CMAKE_SOURCE_DIR}/src/lua-bindings/autocomplete.json
	)
	ADD_CUSTOM_COMMAND(
		COMMAND ${LUAJIT_EXE} ${CMAKE_SOURCE_DIR}/bindings2/generate_bindings.lua
		DEPENDS ${BindSource}
		OUTPUT ${BindOutput}
		WORKING_DIRECTORY ${CMAKE_SOURCE_DIR}
		COMMENT "Generating bindings"
	)
ENDIF()

ADD_LIBRARY(imgui
	STATIC
	deps/libimgui/imconfig.h
	deps/libimgui/imgui.cpp
	deps/libimgui/imgui.h
	deps/libimgui/imgui_demo.cpp
	deps/libimgui/imgui_draw.cpp
	deps/libimgui/imgui_internal.h
	deps/libimgui/imgui_tables.cpp
	deps/libimgui/imgui_widgets.cpp
	deps/libimgui/imstb_rectpack.h
	deps/libimgui/imstb_textedit.h
	deps/libimgui/imstb_truetype.h
	# NOTICE: we do use imgui_stdlib. will maybe inline this in the future
	deps/libimgui/misc/cpp/imgui_stdlib.cpp
	deps/libimgui/misc/cpp/imgui_stdlib.h
)
TARGET_INCLUDE_DIRECTORIES(imgui
	PUBLIC
	deps/libimgui
	deps/libimgui/misc/cpp
)
SET_PROPERTY(TARGET imgui PROPERTY POSITION_INDEPENDENT_CODE ON)

##############################################
# love-imgui: prebuilt integration with love2d
##############################################
if(EnableLove)
	find_package(LuaJIT REQUIRED)
	ADD_LIBRARY(love-imgui
		MODULE
		src/love-imgui/imgui_impl.cpp
		src/love-imgui/imgui_impl.h
		src/love-imgui/wrap_imgui_impl.cpp
		src/love-imgui/wrap_imgui_impl.h

		src/lua-bindings/wrap_imgui_codegen.cpp
		src/lua-bindings/wrap_imgui_codegen.h
	)

	TARGET_INCLUDE_DIRECTORIES(love-imgui
		PUBLIC
		${LUAJIT_INCLUDE_DIR}
		src/lua-bindings
	)

	TARGET_LINK_LIBRARIES(love-imgui ${LUAJIT_LIBRARY} imgui)

	SET_PROPERTY(TARGET love-imgui PROPERTY CXX_STANDARD 17)
	SET_TARGET_PROPERTIES(love-imgui PROPERTIES PREFIX "")
	if(MSVC)
	else()
	  TARGET_COMPILE_OPTIONS(love-imgui PRIVATE -Wconversion)
	  SET_SOURCE_FILES_PROPERTIES(src/lua-bindings/wrap_imgui_codegen.cpp PROPERTIES COMPILE_FLAGS "-Wno-unused-function -Wno-unused-parameter")
	endif()

	INSTALL(TARGETS love-imgui LIBRARY DESTINATION lib)
endif()
